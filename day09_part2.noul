whole_input := read_file(
    __file__.path_parent
    path_join "inputs"
    path_join "input_day09.txt"
);


ROPE_LENGTH := 2;
half_extent := 500;
tail_visited := [[0] ** (half_extent * 2)] ** (half_extent * 2);
tail_visited[half_extent][half_extent] = 1;
rope_positions := V(0, 0) ** ROPE_LENGTH;

for (line <- lines whole_input) (
    dir, amt := words line;
    d := int(amt);
    switch (dir)
        case "U" -> (rope_positions[0] += V(0, d))
        case "D" -> (rope_positions[0] += V(0, -d))
        case "L" -> (rope_positions[0] += V(-d, 0))
        case "R" -> (rope_positions[0] += V(d, 0))
        ;
    delta := rope_positions[0] - rope_positions[1];
    while ((abs(delta[0]) > 1) or (abs(delta[1]) > 1)) (
        # only ever need to make one diagonal step
        rope_positions[1] += if (delta[0] != 0 and delta[1] != 0) V(
            (if (delta[0] > 0) 1 else -1),
            (if (delta[1] > 0) 1 else -1)
        ) else V(
            (if (delta[0] > 0) 1 else if (delta[0] < 0) -1 else 0),
            (if (delta[1] > 0) 1 else if (delta[1] < 0) -1 else 0)
        );
        delta = rope_positions[0] - rope_positions[1];
        tail_visited[
            half_extent + rope_positions[1][0]
        ][
            half_extent + rope_positions[1][1]
        ] = 1;
    );
);

print (sum (tail_visited map sum));
